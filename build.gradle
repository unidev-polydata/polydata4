plugins {
    id 'io.franzbecker.gradle-lombok' version '1.8'
    id "com.palantir.git-version" version "0.12.3"
}

println 'Building version: ' + gitVersion()

subprojects {

    group = 'com.unidev.polydata'
    version = gitVersion()

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'java-library'

    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url "https://mvn.universal-development.com/public"
        }
    }


    dependencies {

        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

        testImplementation 'org.mockito:mockito-all:1.10.19'
        testImplementation 'org.hamcrest:hamcrest-all:1.3'
        testImplementation 'org.assertj:assertj-core:3.9.1'

        testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    }

    idea {
        module {
            downloadSources = true
        }
    }

    tasks.withType(JavaCompile) {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    test {
        useJUnitPlatform()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }
}

